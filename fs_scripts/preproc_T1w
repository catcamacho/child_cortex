#! /bin/csh

# Usage: preproc_T1w studyName subjectFolder [subjectFolder subjectFolder...]
# subjectFolder =  the subject's folder name (e.g. 001-T1). Multiple can be entered at once.

setenv SUBJECTS_DIR /home/camachocm2/Analysis/aggregate_anats/temp_Subjdir
set tempraw = /moochie/user_data/CamachoCat/Aggregate_anats/tempRawDir

switch ( $argv[1] )
case "dibs":
	set rawfp = /moochie/study_data/DIBS/MRI_Data
	set prefix = D
	set file = T1w_MPR
	breaksw
case "chec":
	set rawfp = /moochie/study_data/ChEC
	set prefix = C
	set file = SAG-MPRAGE
	breaksw
case "emogrow":
	set rawfp = /moochie/study_data/EmoGrow/MRI_Data
	set prefix = E
	set file = SAG-MPRAGE
	breaksw
endsw

shift

foreach sub ($argv)

	mkdir ${tempraw}/${prefix}${sub}
	cp -r ${rawfp}/${sub}/${file}* ${tempraw}/${prefix}${sub}
	
	set raw_dir = ${tempraw}/${prefix}${sub}/${file}*
	set raw_dir = $raw_dir[$#raw_dir]
	
	set dicoms = ($raw_dir/*)
	mri_convert -i $dicoms[1] -o ${raw_dir}/mprage.nii.gz
	
	set rawdata = ${raw_dir}/mprage.nii.gz

	if (-e $rawdata) then
		echo '****************************************************************** '
		echo '***************** Working on subject '$sub'********************* '
		echo '****************************************************************** '

		recon-all -s ${prefix}${sub} -i $rawdata -autorecon1 -gcut

		echo '****************************************************************** '
		echo '****************** finished subject '$sub'********************** '
		echo '****************************************************************** '
	else
		echo
		echo '>> WARNIING:'$sub' not found <<' 
		echo '>> WARNIING:'$sub' not found <<' >> $SUBJECTS_DIR/log.txt
		echo 
	endif
end

